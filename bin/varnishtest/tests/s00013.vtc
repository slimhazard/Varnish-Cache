varnishtest "req_total_timeout"

varnish v1 -arg "-p req_total_timeout=1" -vcl {
	import vtc;
	backend b None;

	sub vcl_recv {
		if (req.http.Delay == "recv") {
			vtc.sleep(1.1s);
		}
	}

	sub vcl_hash {
		if (req.http.Delay == "hash") {
			vtc.sleep(1.1s);
		}
	}
} -start

varnish v1 -cliexpect "(?m)Value is: 1.000" "param.show req_total_timeout"

client c1 {
	txreq -hdr "Delay: recv"
	rxresp
	expect resp.status == 503

	txreq -hdr "Delay: hash"
	rxresp
	expect resp.status == 503
} -run

logexpect l1 -v v1 -d 1 -q "Timestamp:Timeout" {
	expect 0 *	Begin     req
	expect * =	ReqHeader "Delay: recv"
	expect * =	Timestamp Timeout
	expect 0 =	Error     "req_total_timeout elapsed"
	expect * =	End

	expect 0 *	Begin     req
	expect * =	ReqHeader "Delay: hash"
	expect * =	Timestamp Timeout
	expect 0 =	Error     "req_total_timeout elapsed"
	expect * =	End
} -run

server s1 {
	rxreq
	txresp
	expect_close

	accept
	rxreq
	txresp
} -start

varnish v1 -vcl+backend {
	import vtc;

	sub vcl_backend_fetch {
		if (bereq.http.Delay == "fetch") {
			vtc.sleep(1.1s);
		}
	}

	sub vcl_backend_response {
		set beresp.do_stream = false;
		if (bereq.http.Delay == "response") {
			vtc.sleep(1.1s);
		}
	}
}

logexpect l1 -v v1 -d 0 -q {Begin ~ "^bereq" and Timestamp:Timeout} {
	expect 0 *	Begin       bereq
	expect * =	BereqHeader "Delay: fetch"
	expect * =	Timestamp   Timeout
	expect 0 =	FetchError  "req_total_timeout elapsed"
	expect * =	End

	expect 0 *	Begin       bereq
	expect * =	BereqHeader "Delay: response"
	expect * =	Timestamp   Timeout
	expect 0 =	FetchError  "req_total_timeout elapsed"
	expect * =	End
} -start

client c1 {
	txreq -hdr "Delay: fetch"
	rxresp
	expect resp.status == 503

	txreq -hdr "Delay: response"
	rxresp
	expect resp.status == 503
} -run

logexpect l1 -wait

server s1 -wait
server s1 {
	rxreq
	delay 1.1
	txresp
} -start

varnish v1 -vcl+backend {}

# XXX: may report FetchError req_total_timeout, first byte timeout, or both
logexpect l1 -v v1 -d 0 -q FetchError {
	expect 0 *	Begin       bereq
	expect * =	FetchError  {^(req_total_|first byte )timeout}
	expect * =	End
} -start

client c1 {
	txreq
	rxresp
	expect resp.status == 503
} -run

logexpect l1 -wait

# req_total_timeout elapse during VFP execution (chunked fetch)
server s1 -wait
server s1 {
	rxreq
	txresp -nolen -hdr "Transfer-Encoding: chunked"
	chunkedlen 10
	delay 1.1
} -start

varnish v1 -vcl+backend {}

logexpect l1 -v v1 -d 0 -q FetchError {
	expect 0 *	Begin       bereq
	expect * =	FetchError  "req_total_timeout elapsed"
	expect * =	End
} -start

client c1 {
	txreq
	expect_close
} -run

logexpect l1 -wait
