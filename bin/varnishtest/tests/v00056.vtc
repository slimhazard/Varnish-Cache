varnishtest "read client.identity causes VCL fail if unset & client addr is UDS"

varnish v1 -arg "-a ${tmpdir}/v1.sock" -vcl {
	backend b { .host = "${bad_ip}"; }

	sub vcl_recv {
		if (req.url == "/nobody") {
			set client.identity = "Samuel B. Nobody";
		}
		set req.http.id = client.identity;
		return(synth(200));
	}

	sub vcl_synth {
		set resp.http.id = req.http.id;
	}
} -start

client c1 -connect "${tmpdir}/v1.sock" {
	txreq -url "/nobody"
	rxresp
	expect resp.status == 200
	expect resp.http.id == "Samuel B. Nobody"
	txreq
	rxresp
	expect resp.status == 503
	expect resp.http.id == ""
} -run

logexpect l1 -v v1 -d 1 -g vxid -q "VCL_Error" {
	expect 0 * Begin req
	expect * = VCL_Error "client.identity not set and there is no client IP"
	expect * = End
} -run
